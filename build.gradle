buildscript {
  ext.kotlinVersion = '1.7.0'
  ext.kotlinCoroutinesVersion = '1.6.3-native-mt'
  ext.ktorVersion = '1.6.8'
  ext.testGsonVersion = '2.9.0'
  ext.testLog4j2Version = '2.17.2'
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

plugins {
  id "com.github.johnrengelman.shadow" version "7.0.0"
}

repositories {
  mavenCentral()
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'kotlin'

def ghidraInstallDir
if (System.env.GHIDRA_INSTALL_DIR) {
  ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
} else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
  ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}
if (!ghidraInstallDir) {
  throw new GradleException("GHIDRA_INSTALL_DIR is not defined!")
}
def allegrexOut = ghidraInstallDir + '/Ghidra/Processors/Allegrex'

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinCoroutinesVersion"
  implementation "io.ktor:ktor-client-core:$ktorVersion"
  implementation "io.ktor:ktor-client-cio:$ktorVersion"
  implementation("io.ktor:ktor-client-gson:$ktorVersion") {
    exclude group: 'com.google.code.gson', module: 'gson' // already provided by Ghidra
  }
  implementation "io.ktor:ktor-client-websockets:$ktorVersion"
  testImplementation "com.google.code.gson:gson:$testGsonVersion"
  testImplementation "org.apache.logging.log4j:log4j-api:$testLog4j2Version"
  testImplementation "org.apache.logging.log4j:log4j-core:$testLog4j2Version"
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Framework', include: "**/*.jar")
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Features', include: "**/*.jar")
  // Debugger
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Debug/Framework-Debugging', include: "**/*.jar")
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Debug/Framework-AsyncComm', include: "**/*.jar")
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Debug/Framework-TraceModeling', include: "**/*.jar")
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Debug/ProposedUtils', include: "**/*.jar")
  shadow fileTree(dir: ghidraInstallDir + '/Ghidra/Debug/Debugger', include: "**/*.jar")
}

compileKotlin {
  kotlinOptions {
    jvmTarget = "11"
  }
}

jar {
  manifest {
    attributes(
      'Specification-Title': "Allegrex",
      'Specification-Version': "10.1.4",
    )
  }
}

//noinspection GroovyAssignabilityCheck
task ghidraInstall {
  dependsOn 'shadowJar'
  doLast {
    delete allegrexOut
    copy {
      from "data"
      into allegrexOut + "/data"
    }
    copy {
      from "ghidra_scripts"
      into allegrexOut + "/ghidra_scripts"
    }
    copy {
      from "build/libs"
      into allegrexOut + "/lib"
      include "ghidra-allegrex-all.jar"
      rename("ghidra-allegrex-all.jar", "Allegrex.jar")
    }
    copy {
      from "."
      into allegrexOut
      include "Module.manifest"
    }
  }
}

task ghidraInstallThenRun {
  dependsOn 'ghidraInstall'
  doLast {
    exec {
      if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'ghidraRun.bat'
      } else {
        commandLine './ghidraRun'
      }
      workingDir ghidraInstallDir
      ignoreExitValue true
    }
  }
}

task ghidraInstallThenDebug {
  dependsOn 'ghidraInstall'
  doLast {
    exec {
      if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'support\\ghidraDebug.bat'
      } else {
        commandLine './support/ghidraDebug'
      }
      workingDir ghidraInstallDir
      ignoreExitValue true
    }
  }
}

task ghidraInstallThenCompile {
  dependsOn 'ghidraInstall'
  doLast {
    exec {
      if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', 'support\\sleigh.bat', '-a', 'Ghidra\\Processors\\Allegrex\\data\\languages'
      } else {
        commandLine './support/sleigh', '-a', './Ghidra/Processors/Allegrex/data/languages'
      }
      workingDir ghidraInstallDir
      ignoreExitValue false
    }
  }
}

task ghidraInstallThenPackage(type: Zip) {
  dependsOn 'ghidraInstallThenCompile'
  archiveFileName = "Allegrex.zip"
  destinationDirectory = file("$buildDir/dist")

  from allegrexOut
  into "Allegrex"
}
